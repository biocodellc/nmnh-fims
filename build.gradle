buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.ajoberstar:gradle-git:1.4.2"
        classpath "com.github.marceloemanoel:gradle-environments-plugin:0.1"
        classpath "org.hidetake:gradle-ssh-plugin:2.0.0"
    }
}

import org.ajoberstar.grgit.*
import groovy.io.FileType

apply plugin: "java"
apply plugin: "war"
apply plugin: "idea"
apply plugin: "environments"
apply plugin: "org.hidetake.ssh"

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
configurations {
    server
}

// this is necessary for the fatWar task subprojects.each closure to work correctly
subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }

idea {
    module {
        scopes.COMPILE.plus += [configurations.server]
    }
}

//noinspection GroovyAssignabilityCheck
sourceSets {
    main {
        compileClasspath += [configurations.server]
    }
    test {
        compileClasspath += [configurations.server]
        runtimeClasspath += [configurations.server]
    }
}

dependencies {
    server group: 'wsdl4j', name: 'wsdl4j', version: '1.6.3'
    server 'org.glassfish.jersey.media:jersey-media-multipart:2.25'
    server 'commons-io:commons-io:2.4'
    server 'org.apache.axis:axis-jaxrpc:1.4'
    server 'org.apache.axis:axis:1.4'
    server group: 'commons-discovery', name: 'commons-discovery', version: '0.5'
    server fileTree(dir: 'lib', include: ['*.jar'])

    server('org.glassfish.jersey.ext:jersey-spring3:2.25') {
        exclude module: 'bean-validator'
        exclude module: 'asm-all-repackaged'
    }
    server 'javax.servlet:servlet-api:2.4'
    server 'org.springframework.data:spring-data-jpa:1.10.1.RELEASE'
    server group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    server 'org.apache.commons:commons-digester3:3.2'
    server group: 'commons-collections', name: 'commons-collections', version: '3.2.1'
    server 'org.apache.poi:poi:3.10.1'
    server 'org.apache.poi:poi-ooxml:3.10.1'
    server 'com.googlecode.json-simple:json-simple:1.1.1'
    server 'commons-lang:commons-lang:2.6'
    server 'commons-cli:commons-cli:1.2'

    compile project(":biocode-fims-commons")

    testCompile "junit:junit-dep:4.10"
}

webAppDirName = "src/main/web"
libsDirName = "../dist"

project.ext.environment = defaultEnvironment

//noinspection GroovyAssignabilityCheck
clean {
    delete libsDir
}

// jetty ssh plugin
remotes {
    nmnh {
        host = 'nmnh-fims.si.edu' 
        user = nmnhUser
        password = nmnhPassword
    }
    nmnhdev {
        host = 'nmnh-rcisdev2.si.edu'
        user = nmnhDevUser
        password = nmnhDevPassword
    }
}

allprojects {
    task verifyMasterBranch << {
        ext.repo = Grgit.open(project.file('.'))
        if (ext.repo.branch.current.name != "master")
            throw new GradleScriptException(project.name + ' is not on the master branch', null)
    }
}

task setProductionEnv << {
    project.ext.environment = "production"
}
task setDevelopmentEnv << {
    project.ext.environment = "development"
}
task setTrainingEnv << {
    project.ext.environment = "training"
}
task setLocalEnv << {
    project.ext.environment = "local"
}

// if setProductionEnv or setDevelopmentEnv is run, make sure this is done before processResources
setProductionEnv.mustRunAfter compileJava
setDevelopmentEnv.mustRunAfter compileJava
setTrainingEnv.mustRunAfter compileJava
setLocalEnv.mustRunAfter compileJava

/*
Delete any resource files in src/main/resources if the file exists in a sub directory
of src/main/environment, excluding the current environment setting directory
*/
task cleanResourceDirectory {
    doLast {
        def allResourceFilesToRemove = []

        def environmentDir = new File("src/main/environment")
        environmentDir.eachFileRecurse(FileType.FILES) { file ->
            allResourceFilesToRemove << file.name
        }

        allResourceFilesToRemove.each {
            def resourceFile = new File("src/main/resources/${it}")
            if (resourceFile.exists()) {
                resourceFile.delete()
            }
        }
    }
}

task copyEnvironmentResources {
    dependsOn cleanResourceDirectory
    doLast {
        project.copy {
            from "src/main/environment/${project.environment}"
            into "src/main/resources"
            include "**/*"
            exclude "web.xml"
            exclude ".gitkeep"
        }
    }
}
processResources.dependsOn copyEnvironmentResources

task copyWebXml {
    doLast {
        project.copy {
            from "src/main/environment/${project.environment}"
            into "src/main/web/WEB-INF"
            include "web.xml"
        }
    }
}

processResources.dependsOn copyWebXml

ext.updateRemoteDependencies = { location ->
    subprojects.each { subproject ->
        project.configurations.server.dependencies.addAll(subproject.configurations.server.dependencies)
    }

    ssh.run {
        session(remotes.biscicol) {
            execute "/bin/rm -f " + location + "*"
            put from: project.configurations.server.incoming.getFiles(), into: location
        }
    }
}

task updateDependenciesDev {
    group = "biscicol.org"
    description = "update the jars located in " + remoteDevLibsDirectory

    doLast {
        updateRemoteDependencies(remoteDevLibsDirectory)
    }
}

task updateDependencies {
    group = "biscicol.org"
    description = "update the jars located in " + remoteLibsDirectory

    dependsOn verifyMasterBranch

    doLast {
        updateRemoteDependencies(remoteLibsDirectory)
    }
}

ext.remoteDeploy = { workingDir, remoteLibsDir, remoteWarDir, remoteConn ->
    ssh.run {
        session(remoteConn) {
            // Cleanup to begin
            execute "/bin/rm -rf " + workingDir
            execute "/bin/mkdir " + workingDir
            // Copy war file to server
            put from: war.archivePath.path, into: workingDir
            // Extract libs and copy from server
            execute "/usr/bin/unzip -d " + workingDir + " " + workingDir + war.archiveName
            execute "/bin/cp " + remoteLibsDir + "* " + workingDir + "WEB-INF/lib/"
            // Remove the old war file
            execute "/bin/rm " + workingDir + war.archiveName
            // Need to remove the war file itself-- we instead just copy it into /tmp for safekeeping in case next step fails
            execute "/bin/mv " + remoteWarDir + "fims.war" + " /tmp/fims.deployed.war.backup"
            // Need to execute zip command from the working Directory in order to get relative paths correct
            execute "cd " + workingDir + ";/usr/bin/zip -r " + remoteWarDir + "fims.war " + " ."
            // Cleanup
            execute "/bin/rm -rf " + workingDir
            execute "/bin/rm /tmp/fims.deployed.war.backup"
        }
    }
}

task deployFims {
    group = 'nmnh'
    description = 'verify that all subprojects are on the master branch. Then build and copy war to nmnh production jetty webApp directory'

    dependsOn verifyMasterBranch
    dependsOn setProductionEnv
    dependsOn war

    doLast {
        def workingDir = "/tmp/fimsProd/"
        remoteDeploy(workingDir, remoteLibsDirectory, remoteWarDirectory, remotes.nmnh)
    }
}

task deployFimsDev {
    group = 'nmnh'
    description = 'build and copy war to nmnh development jetty webApp directory'

    dependsOn setDevelopmentEnv
    dependsOn war

    doLast {
        def workingDir = "/tmp/fimsDev/"
        remoteDeploy(workingDir, remoteDevLibsDirectory, remoteWarDevDirectory, remotes.nmnhdev)
    }
}

task deployFimsTraining {
    group = 'nmnh'
    description = 'build and copy war to nmnh development jetty webApp directory'

    dependsOn setTrainingEnv
    dependsOn war

    doLast {
        def workingDir = "/tmp/fimsTraining/"
        remoteDeploy(workingDir, remoteTrainingLibsDirectory, remoteWarTrainingDirectory, remotes.nmnhdev)

    }
}

task fatWar(type: War) {
    archiveName = "nmnh-fims-fat.war"

    subprojects.each { subproject ->
        project.configurations.server.dependencies.addAll(subproject.configurations.server.dependencies)
    }

    classpath configurations.server
    classpath configurations.compile
}

task deployFimsLocal  {
    group = 'nmnh'
    description = 'local fims deployment'

    dependsOn setLocalEnv
    dependsOn fatWar

    doLast {
        project.copy {
            from fatWar.destinationDir
            into localWarDirectory
            include fatWar.archiveName
            rename fatWar.archiveName, "fims.war"
        }
    }
}
